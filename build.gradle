buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
    mavenCentral()
}

def allureVersion = "2.13.8",
    selenideVersion = "5.20.1",
    junitVersion = "5.7.0",
    allureEE = "3.39.5",
    restAssured = "4.2.0",
    jackson = "2.11.2",
    lombok = "1.18.18"


allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    configuration = "testImplementation"

    useJUnit5 {
        version = "${allureVersion}"
    }
}

dependencies {
    testImplementation "com.codeborne:selenide:${selenideVersion}"

    testImplementation "io.qameta.allure:allure-selenide:${allureVersion}"
    testImplementation "io.qameta.allure:allure-rest-assured:${allureVersion}"
    testImplementation "io.qameta.allure:allure-ee-junit-platform:${allureEE}"

    testImplementation "io.rest-assured:rest-assured:${restAssured}"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jackson}"

    testCompileOnly "org.projectlombok:lombok:${lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok}"

    testImplementation "org.aeonbits.owner:owner:1.0.12"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "org.aspectj:aspectjweaver:1.9.5"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties += System.properties
    systemProperty "junit.jupiter.extensions.autodetection.enabled", "true"

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.web.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.web.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }
    testLogging {
        lifecycle {
            events "passed", "failed"
//            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
}

task api(type: Test) {
    useJUnitPlatform {
        includeTags 'api'
    }
}